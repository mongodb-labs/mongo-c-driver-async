@PACKAGE_INIT@

if(NOT "${amongoc_FIND_COMPONENTS}" STREQUAL "")
    message(SEND_ERROR "No COMPONENTS are supported for find_package(amongoc)")
endif()

string(JOIN " " __help
    "For imported amongoc: If enabled (the default), we will import dependency"
    "packages using the `find_dependency` macro. If disabled, then it is up to"
    "the importer to define the required import targets needed by the amongoc library.")
option(AMONGOC_FIND_DEPENDENCIES "${__help}" ON)
mark_as_advanced(AMONGOC_FIND_DEPENDENCIES)
if(AMONGOC_FIND_DEPENDENCIES)
    include(CMakeFindDependencyMacro)
    find_dependency(asio)
    find_dependency(Boost CONFIG COMPONENTS URL Container)
    find_dependency(fmt)
endif()

# Target exports file that is install alongside this package-config file
include("${CMAKE_CURRENT_LIST_DIR}/amongoc-targets.cmake")
